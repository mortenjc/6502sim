100 PRINTCHR$(147):POKE53280,0:POKE53281,0:C1=0:K1=0:L1=0:B1=0:W1=0:J1=0:S1=0
110 DIM N$(20) ,L(27), M(11,4), R$(11)
120 GOSUB 500:L=1:PRINT"I HEAR A RASPING NOISE":FORTD=1 TO 1000:NEXT TD
125 PRINT"I HEAR A HORSE!":FOR TD=1TO3000:NEXTTD
126 PRINT:FORCY=1TO10:POKE646,CY*3:PRINT"  CLOPPITY":FORTD=1TO200:NEXTTD
128 NEXTCY
130 GOSUB 700
132 IFN=13ANDL=10THENPRINT:PRINT"HE'S A DANGEROUS OUTLAW. BE CAREFUL"
135 IFM(4,2)<>0ORL(13)<>3THEN140
136 PRINTCHR$(147):PRINT"YOU CAUGHT BLACK BART!"
137 PRINT"THE REWARD IS YOURS!"
138 END
140 REM
142 POKE646,13:PRINT:PRINT"TELL ME WHAT TO DO":GOSUB1000
145 IF S=1THENS=0:GOTO140
150 FOR I=1 TO LEN(V$)STEP2
160 IF MID$(V$,I,2)=A$THENV=(I+1)/2:GOTO200
170 NEXTI
180 PRINT"I DON'T KNOW HOW TO DO THAT":GOTO140
200 IF B$="" THEN 300
210 FOR I=1TOLEN(N$)STEP2
220 IF MID$(N$,I,2)<>B$THEN 230
225 N=(I+1)/2
227 IFN=21THENN=9
229 GOTO300
230 NEXTI
300 ON V GOTO 350,400,400,450,480,800,800,830,130,800,900,980,950
309 REM UNLOCK DRAWER DESK OR CELL
310 IF N=6ANDL(17)=0ANDL=2THENJ1=1:M(2,3)=3:GOSUB1150:M(3,4)=2:GOTO130
320 PRINT"CAN'T DO THAT YET"
330 GOTO130
350 IFN<>7 AND N< 22 THEN 380
355 IF N=7 ANDM(4,2)<>0AND(L=5ORL=3)THENL=4:GOTO130
360 IFM(L,N-21)=0THEN395
370 L=M(L,N-21):GOTO130
380 IF N<>4 OR L<>7THEN 385
381 PRINT"SPLASH--I'M ALL WET":L(18)=7
382 IFL(11)=50THENPRINT"I FOUND SOMETHING":L(11)=7:GOTO130
385 IFN=5 ANDL=9THENL=10:GOTO130
395 PRINT"CAN'T GO THAT WAY":GOTO130
400 IFC>=4THENPRINT"GO TOO MUCH--TAKE INVENTORY":GOTO140
405 IFN=14THEN425
410 IFL(N)<>LTHENPRINT"I DON'T SEE IT HERE":GOTO140
420 IFN<8ORN>19THENPRINT"CAN'T TAKE THAT":GOTO140
425 IFN=14ANDL<>2ANDL(16)<>0 THENPRINT"CAN'T DO THAT YET":GOTO140
430 IFN=14ANDL=2THENN$(3)="SINK FULL OF WATER":W1=1:GOTO130
435 IFN=14ANDL(16)=0THENN$(16)="BUCKET OF WATER":W1=1:GOTO130
440 IF N=13AND S1=1 THEN 444
441 IFN<>13THEN444
442 PRINT"BLACK BART GETS MAD!":PRINT"HE DRAWS HIS REVOLVER!"
443 PRINT"THAT'S THE END":END
444 IFN=13ANDL(11)<>0THENPRINT"CAN'T DO THAT YET":GOTO140
445 PRINT"OKAY":C=C+1:L(N)=0:GOTO140
450 IFL1=1THENPRINT"IT'S NOT LOCKED":GOTO140
460 IFL=2ANDL(9)=0ANDL1=0AND(N=2ORN=20ORN=26)THEN465
462 GOTO470
465 L1=1:K1=1:PRINT"OKAY":N$(2)="UNLOCKED DESK"
470 GOTO140
480 IFN<>2ANDN<>20ANDN<>26THENPRINT"CAN'T":GOTO140
490 IFK1=1ANDL=2ANDL(17)=50THENL(17)=2:GOTO495
492 PRINT"CAN'T":GOTO140
495 PRINT"OKAY--THERE'S SOMETHING IN THERE":GOTO140
500 FORI=1TO20:READN$(I):NEXTI
510 DATAPOSTER,DESK WITH LOCKED DRAWER,SINK,HORSE TROUGH,ROOM IN BACK
511 DATA JAIL CELL-LOCKED,EMPTY WINDOW,BARS, BOBBY PIN, SACK OF CEMENT
512 DATA SHINY STAR,BOTTLE OF ELIXIR,BLACK BART,WATER,WATER,BUCKET,KEY
514 DATA WATER,EMPTY WINDOW,CACTUS
550 V1$="NSEW"
560 V$="GOTAGEPIOPPUDRMILOGISWFIREUN"
570 N$="PODESITRROJAWIBABOCESTELBLWAWABUKEWAWIDRPINOSOEAWELO"
600 FORI=1TO20:READL(I):NEXTI
610 DATA1,2,3,7,9,2,3,5,8,8,50,9,10,50,99,7,50,99,5,11
620 FORI=1TO11:FORJ=1TO4:READM(I,J):NEXTJ:NEXTI
630 DATA 7,6,2,,,,,1,,,,0,3,5,,,0,,,6
640 DATA 1,,5,11,,1,8,9,,,,7,,,7,,,,9,
642 DATA 0,0,6,0
650 FORI=1TO11:READR$(I):NEXTI
660 DATA DUSTY STREET,OFFICE,JAIL,WINDOW,DUSTY STREET BEHIND JAIL
670 DATA STREET CORNER,DUSTY STREET,GENERAL STORE,SALOON,ROOM IN BACK
672 DATA DESERT ROAD
675 R$(2)="SHERIFF'S "+R$(2)
680 RETURN
700 PRINTCHR$(147):PRINT:PRINT:PRINT"AI AM IN A _"R$(L)
710 PRINT".I SEE A";
720 FOR I=1 TO 20
730 IF L(I)=L THEN PRINTN$(I);" ";:PRINT:S=1
740 NEXT I
750 IF S=0 THEN PRINT"NOTHING SPECIAL"
760 IF S=1 THEN PRINT:S=0
765 IF L=0THENRETURN
770 PRINT".OBVIOUS EXITS ARE .";
775 FORI=1TO4:IFM(L,I)=0THEN785
780 PRINTMID$(V1$,I,1)+" ";
785 NEXTI:PRINT
790 RETURN
800 IFN=10ORN=8THEN830
805 IFN=12ANDS1=0THEN870
810 IFL(N)=0THENL(N)=L:C=C-1:PRINT"OKAY":GOTO130
820 PRINT"I'M NOT CARRYING IT":GOTO140
830 PRINT"WHERE":INPUT C$
840 IFLEFT$(C$,2)<>"SI"ANDLEFT$(C$,2)<>"WA"ANDLEFT$(C$,2)<>"BU"THEN 845
842 IFW1=1ANDN=10ANDL(10)=0THEN850
843 GOTO847
845 IFLEFT$(C$,2)="WI"ANDL(8)=0ANDC1=1ANDL=4ANDN=8 THEN860
847 PRINT"CAN'T DO THAT YET":GOTO140
850 PRINT"OKAY.  IT'S MIXED":C1=1:L(10)=50:C=C-1
855 IFLEFT$(C$,2)="BU"THENN$(16)="BUCKET OF CEMENT MIXTURE":GOTO130
856 N$(15)="CEMENT MIXTURE":L(15)=L:GOTO130
860 PRINT"OKAY":N$(7)="BARRED WINDOW":L=3:M(4,2)=0:L(8)=50:C=C-1:GOTO130
870 IFL(12)<>0THEN847
875 IFA$<>"GI"THEN900
876 IFL<>10THEN847
880 L(12)=50:PRINT"BLACK BART GULPS ELIXIR AND IMMEDIATELYFALLS ASLEEP"
890 S1=1:C=C-1:N$(13)="SLEEPING "+N$(13):GOTO140
900 IFN=12ANDL(12)=0THENL(12)=50:PRINT"GLUG GLUG GLUG":C=C-1:GOTO140
910 IFN=12 THENPRINT"CAN'T DO THAT YET":GOTO140
920 PRINT"CAN'T DRINK THAT":GOTO140
950 IFN<>1ORL<>1THENPRINT"CAN'T":GOTO140
960 PRINT"WANTED ALIVE-BLACK BART! $1000REWARD"
970 GOTO140
980 IFN<>3ORL<>3THENPRINT"CAN'T":GOTO140
990 PRINT"HOW";:INPUTC$
995 IFLEFT$(C$,2)="WA"THENL(14)=3:W1=1
996 GOTO130
1000 INPUT A$:B$=""
1005 IF LEFT$(A$,2)="QU"THENEND
1010 IFLEN(A$)=1THEN1060
1020 FORI=1TOLEN(A$)
1030 IFMID$(A$,I,1)=" "AND LEN(A$)>I+1THENB$=MID$(A$,I+1,2):GOTO1045
1040 NEXTI
1045 A$=LEFT$(A$,2):RETURN
1060 IFA$<>"I"THEN1100
1070 PRINT"I'M CARRYING ";:T=L:L=0:GOSUB720
1080 L=T:S=1:RETURN
1100 FORI=1TO4
1110 IFA$<>MID$(V1$,I,1)THEN1130
1120 A$="GO":B$=MID$(N$,2*I+41,2):RETURN
1130 NEXTI
1140 PRINT"CAN'T DO THAT":S=1:RETURN
1150 N$(6)="OPEN JAIL CELL":RETURN
